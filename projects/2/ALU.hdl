// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //solving columns of worksheet
    //chain columns together
    //code what it does then add a mux to decide to do it
    //multiple outputs

//zx
        Mux16(a= x, b= false, sel= zx, out= x1);
        Not16(in= x1, out= notx);
        Mux16(a= x1, b= notx, sel= nx, out= x2);
//zy
        Mux16(a= y, b= false, sel= zy, out= y1);
        Not16(in= y1, out= noty);
        Mux16(a= y1, b= noty, sel= ny, out= y2);
//f
        And16(a= x2, b= y2, out= xandy);
        Add16(a= x2, b= y2, out= xplusy);
        Mux16(a= xandy, b= xplusy, sel= f, out= fout);
//no
        Not16(in= fout, out= nofout);
        Mux16(a= fout, b= nofout, sel= no, out= out, out[15]= out1, out[0..7]= first8, out[8..15]= last8);
//ng
        And(a= out1, b= true, out= ng);
//zr
        Or8Way(in= first8, out= zr1);
        Or8Way(in= last8, out= zr2);
        Or(a= zr1, b= zr2, out=nzr );
        Not(in= nzr, out= zr);


}